# Maintainer: Duama <admin@duama.top>

pkgname=filebrowser
pkgver=2.15.0
pkgrel=1
pkgdesc='Web File Browser'
arch=(x86_64)
url='https://github.com/filebrowser/filebrowser'
license=(Apache)
depends=(glibc)
makedepends=(npm go go.rice)
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz"
        "$pkgname@.service")
sha256sums=('acae93f135fa2c10114c8f5eeb51a80b1ef3bcdeb0eb7c0d4849b890196c95b4'
            '31ec64a8e47b33dac958caaf9bedb1c3d3052194007f770d4c05228d4301bddc')

prepare() {
    cd $pkgname-$pkgver

    # Front end
    cd frontend
    npm install
    cd ..
}

build() {
    cd $pkgname-$pkgver

    # Front end
    cd frontend
    npm run build
    cd ..

    # Back end
    cd http
    rice embed-go
    cd ..

    export CGO_CPPFLAGS="$CPPFLAGS"
    export CGO_CFLAGS="$CFLAGS"
    export CGO_CXXFLAGS="$CXXFLAGS"
    export CGO_LDFLAGS="$LDFLAGS"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    go build .
}

package() {
    install -Dm755 $pkgname-$pkgver/$pkgname "$pkgdir"/usr/bin/$pkgname
    install -Dm755 $pkgname@.service "$pkgdir"/usr/lib/systemd/system/$pkgname@.service
}
