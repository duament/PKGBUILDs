# Maintainer: Duama <admin@duama.top>

pkgname=filebrowser
pkgver=2.20.0
pkgrel=1
pkgdesc='Web File Browser'
arch=(x86_64)
url='https://github.com/filebrowser/filebrowser'
license=(Apache)
depends=(glibc)
makedepends=(npm go go.rice)
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz"
        "$pkgname@.service")
sha256sums=('ad55e74d3dbd9350405c8697c88d70d4dc52a0e1beb7d921ac5adad2328127f4'
            '7ea9a6d8e54e06484956cec4aa84b57d076c4e1fcad16cb9f6e8e7d8597ad64c')

prepare() {
    cd $pkgname-$pkgver

    # Front end
    cd frontend
    npm install
    cd ..
}

build() {
    cd $pkgname-$pkgver

    # Front end
    cd frontend
    npm run build
    cd ..

    # Back end
    cd http
    rice embed-go
    cd ..

    export CGO_CPPFLAGS="$CPPFLAGS"
    export CGO_CFLAGS="$CFLAGS"
    export CGO_CXXFLAGS="$CXXFLAGS"
    export CGO_LDFLAGS="$LDFLAGS"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
    go build .
}

package() {
    install -Dm755 $pkgname-$pkgver/$pkgname "$pkgdir"/usr/bin/$pkgname
    install -Dm644 $pkgname@.service "$pkgdir"/usr/lib/systemd/system/$pkgname@.service
}
