# Maintainer: Rvfg <i@rvf6.com>
# Contributor: Pierre-Alain TORET <pierre-alain.toret@protonmail.com>
# Contributor: Tom Hacohen <tom@stosb.com>

pkgname=etebase-server
_name=server
pkgver=0.8.3
pkgrel=2
pkgdesc="A self-hostable Etebase (EteSync 2.0) server"
arch=(any)
depends=('python-aiofiles'
         'python-aioredis'
         'python-django'
         'python-fastapi'
         'python-msgpack'
         'python-pynacl'
         'uvicorn')
optdepends=('postgresql: storage backend'
            'python-daphne: application container')
url="https://github.com/etesync/server"
license=('AGPL3')
backup=('etc/etebase-server/etebase-server.ini')
source=("$_name-$pkgver.tar.gz::https://github.com/etesync/$_name/archive/v$pkgver.tar.gz"
        "fix-providing_args.patch"
        "fix-django.urls.path.patch"
        "$pkgname.service")
sha256sums=('53680e044c976b862f7dacde72402f86bbff3f7a7455ef517f8ad591a42e0944'
            '7029ffb58f67369ba33a9b5b1ef06907a82e5e03d6d941484acbe1e9702baac2'
            '1c727d0846f397f4f6afdbe91364a11705f3b96b696c81deb1bac2d74adfa676'
            '7d0aa618fe764a36744479991e42a21194450c44f180f9082f103e40bcb9b405')

prepare() {
    cd $_name-$pkgver
    patch --forward --strip=1 --input="$srcdir"/fix-providing_args.patch
    patch --forward --strip=1 --input="$srcdir"/fix-django.urls.path.patch
    sed -i "s@secret.txt@/var/lib/$pkgname/secret.txt@;s@db.sqlite3@/var/lib/$pkgname/db.sqlite3@" $pkgname.ini.example
}

package() {
    install -Dm644 $pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service

    cd $_name-$pkgver

    install -Dm644 $pkgname.ini.example "$pkgdir"/etc/$pkgname/$pkgname.ini
    install -Dm644 README.md "$pkgdir"/usr/share/doc/$pkgname/README.md
    find example-configs -type f -exec install -Dm644 {} "$pkgdir"/usr/share/doc/$pkgname/{} \;
    find . -type f -name '*.py' -exec install -Dm644 {} "$pkgdir"/usr/lib/$pkgname/{} \;
    find templates -type f -exec install -Dm644 {} "$pkgdir"/usr/lib/$pkgname/{} \;
}
