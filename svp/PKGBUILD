# Maintainer: phiresky <phireskyde+git@gmail.com> 
pkgname=svp
pkgver=4.3.191
pkgrel=1
epoch=
pkgdesc="SmoothVideo Project 4 (SVP4)"
arch=('x86_64')
url="https://www.svp-team.com/wiki/SVP:Linux"
license=('custom')
depends=(libmediainfo qt5-svg qt5-script qt5-declarative vapoursynth libusb xdg-utils lsof)
makedepends=(p7zip)
optdepends=(
	'mpv-git: needed for mpv vapoursynth support'
	'ocl-icd: for GPU acceleration'
)
options=(!strip)
source=("https://www.svp-team.com/files/svp4-linux.4.3.191-1.tar.bz2")
sha256sums=('b29970277fe35e8954f3b126b9016d23143be7b23fcfe17d350c59a25f1fa468')

prepare() {
	rm -rf "$srcdir/installer"
	mkdir "$srcdir/installer"
	echo "Finding 7z archives in installer..."
	LANG=C grep --only-matching --byte-offset --binary --text  $'7z\xBC\xAF\x27\x1C' "$srcdir/svp4-linux-64.run" |
		cut -f1 -d: |
		while read ofs; do dd if="$srcdir/svp4-linux-64.run" bs=1M iflag=skip_bytes status=none skip=$ofs of="$srcdir/installer/bin-$ofs.7z"; done

	echo "Extracting 7z archives from installer..."
	for f in "$srcdir/installer/"*.7z; do
		7z -bd -bb0 -y x -o"$srcdir/extracted/" "$f" || true
	done
}

package() {
	mkdir -p "$pkgdir"/{opt/svp,usr/share/licenses/svp}
	if [[ -d "$srcdir/extracted/licenses" ]]; then
		mv "$srcdir/extracted/licenses" "$pkgdir/usr/share/licenses/$pkgname"
	fi
	mv "$srcdir/extracted/"* "$pkgdir/opt/$pkgname"
	#rm "$pkgdir/opt/$pkgname/extensions/libsvpcode.so"
	chmod -R +rX "$pkgdir/opt/svp" "$pkgdir/usr/share"
}
