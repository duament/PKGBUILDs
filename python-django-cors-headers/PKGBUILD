# Maintainer: Duama <admin@duama.top>

pkgname=python-django-cors-headers
_name=${pkgname#python-}
pkgver=3.10.1
pkgrel=1
pkgdesc="Django app for handling the server headers required for Cross-Origin Resource Sharing (CORS)"
arch=('any')
url="https://github.com/adamchainz/django-cors-headers"
license=('MIT')
depends=('python-django')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-django' 'python-pytest-randomly')
source=("$_name-$pkgver.tar.gz::https://github.com/adamchainz/$_name/archive/$pkgver.tar.gz")
sha256sums=('f44b3aa1351e6fb7d25abfa078f2f411364d4eb706d2067f49391e2d67b6bf6b')

build() {
    cd $_name-$pkgver
    python setup.py build
}

check() {
    cd $_name-$pkgver
    PYTHONPATH="$PWD:$PWD/src" pytest
}

package() {
    cd $_name-$pkgver
    python setup.py install --root="$pkgdir" --optimize=1 --skip-build
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

