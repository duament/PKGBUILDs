# Maintainer: Duama <admin@duama.top>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: erk <v at erk dot io>

_vtextedit=2eee68d02b6a48b5064dc76df2b5c7fe94bd8f0c
_sonnet=403863fc499c58677c9b0b25f820d0baa9186a07
pkgname=vnote
pkgver=3.4.0
pkgrel=1
pkgdesc="A pleasant note-taking platform."
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url='https://github.com/vnotex/vnote'
license=(LGPL3)
depends=(qt5-base qt5-webengine qt5-svg syntax-highlighting hunspell)
makedepends=()
source=("$pkgname-$pkgver.tar.gz::https://github.com/vnotex/$pkgname/archive/refs/tags/v$pkgver.tar.gz"
        "vtextedit-$_vtextedit.tar.gz::https://github.com/vnotex/vtextedit/archive/$_vtextedit.tar.gz"
        "sonnet-$_sonnet.tar.gz::https://github.com/vnotex/sonnet/archive/$_sonnet.tar.gz"
        "rcc-dir.patch"
        "vnote-font.patch"
        "vtextedit-font.patch"
        "vtextedit-use-system-libs.patch"
        "sonnet-use-system-libs.patch")
sha256sums=('64d47f8ad6b41bde2059125f2595b96ccd6fa2fc8aa58a2e65255689783ed926'
            '0ffb962d1390bc6db582d0218c2d6c4180b82c2bb0ae5c2c242b4cc930b65c20'
            '1f4bd73c17854611e11f73c3ed4206dbc1768b0343f1101bd1449573b04b64d4'
            '9d94cb20dc61e0463acb10951e1b1828a9c516574b7482aa0db0a4ab4ae1998c'
            'bf37a6f6d1f78f575e0aff493e4f7258329caec702a8d85034a52b151adfa6d3'
            '2bfa47d4008751ba55326141d0c44924596fa0f78bdd6cec4e3809b2812c4ed2'
            '42c710d372dd14c5bbf9d0640284e689510a39d48e14829da68ac4687b91838d'
            '81be51efa75e73280db45e4575ceedc3ddc8c1cd5caab4ebc179c61176ee6e7e')

prepare() {
    cd $pkgname-$pkgver

    rmdir libs/vtextedit
    ln -s "$srcdir"/vtextedit-$_vtextedit libs/vtextedit

    patch --forward --strip=1 --input="$srcdir"/rcc-dir.patch
    patch --forward --strip=1 --input="$srcdir"/vnote-font.patch

    cd ../vtextedit-$_vtextedit

    rmdir src/libs/sonnet
    ln -s "$srcdir"/sonnet-$_sonnet src/libs/sonnet

    patch --forward --strip=1 --input="$srcdir"/vtextedit-use-system-libs.patch
    patch --forward --strip=1 --input="$srcdir"/vtextedit-font.patch

    cd ../sonnet-$_sonnet

    patch --forward --strip=1 --input="$srcdir"/sonnet-use-system-libs.patch
}

build() {
    cd $pkgname-$pkgver

    qmake
    make
}

package() {
    cd $pkgname-$pkgver

    make INSTALL_ROOT="$pkgdir" install
}
