# Maintainer: Duama <admin@duama.top>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: erk <v at erk dot io>

_vtextedit=08b440d3045c944a8a6d7f8945a197ff56dcec82
_sonnet=403863fc499c58677c9b0b25f820d0baa9186a07
pkgname=vnote
pkgver=3.10.1
pkgrel=1
pkgdesc="A pleasant note-taking platform."
arch=(x86_64 i686 arm armv6h armv7h aarch64)
url='https://github.com/vnotex/vnote'
license=(LGPL3)
depends=(qt5-base qt5-webengine qt5-svg syntax-highlighting hunspell)
makedepends=()
source=("$pkgname-$pkgver.tar.gz::https://github.com/vnotex/$pkgname/archive/refs/tags/v$pkgver.tar.gz"
        "vtextedit-$_vtextedit.tar.gz::https://github.com/vnotex/vtextedit/archive/$_vtextedit.tar.gz"
        "sonnet-$_sonnet.tar.gz::https://github.com/vnotex/sonnet/archive/$_sonnet.tar.gz"
        "rcc-dir.patch"
        "vnote-font.patch"
        "vnote-use-system-libs.patch"
        "vtextedit-font.patch"
        "vtextedit-use-system-libs.patch"
        "sonnet-use-system-libs.patch")
sha256sums=('b89719b7650f98824b9266ba11949aa5eacb005c379e2eab566d1f7ca62d064e'
            'c70553a99fd9bbe5b8b7e5c724a744f49493166ffbda668547c88109c539a9bb'
            '1f4bd73c17854611e11f73c3ed4206dbc1768b0343f1101bd1449573b04b64d4'
            '9d94cb20dc61e0463acb10951e1b1828a9c516574b7482aa0db0a4ab4ae1998c'
            '40c9cf04d60de6646d0c229e082dc3fa7280d4307c35555844d0da1a169647ae'
            'b6aa1fa31de0442888c1c089e2189ebb814f3d4aa23b7e815ea35d3569c018c1'
            '2bfa47d4008751ba55326141d0c44924596fa0f78bdd6cec4e3809b2812c4ed2'
            '42c710d372dd14c5bbf9d0640284e689510a39d48e14829da68ac4687b91838d'
            '81be51efa75e73280db45e4575ceedc3ddc8c1cd5caab4ebc179c61176ee6e7e')

prepare() {
    cd $pkgname-$pkgver

    rmdir libs/vtextedit
    ln -s "$srcdir"/vtextedit-$_vtextedit libs/vtextedit

    patch --forward --strip=1 --input="$srcdir"/rcc-dir.patch
    patch --forward --strip=1 --input="$srcdir"/vnote-use-system-libs.patch
    patch --forward --strip=1 --input="$srcdir"/vnote-font.patch

    cd ../vtextedit-$_vtextedit

    rmdir src/libs/sonnet
    ln -s "$srcdir"/sonnet-$_sonnet src/libs/sonnet

    patch --forward --strip=1 --input="$srcdir"/vtextedit-use-system-libs.patch
    patch --forward --strip=1 --input="$srcdir"/vtextedit-font.patch

    cd ../sonnet-$_sonnet

    patch --forward --strip=1 --input="$srcdir"/sonnet-use-system-libs.patch
}

build() {
    cd $pkgname-$pkgver

    qmake
    make
}

package() {
    cd $pkgname-$pkgver

    make INSTALL_ROOT="$pkgdir" install
}
