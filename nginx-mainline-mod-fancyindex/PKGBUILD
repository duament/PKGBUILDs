# Maintainer: Duama <admin@duama.top>
# Contributor: Morgenstern <charles [at] charlesbwise [dot] com>
# Contributor: jkl <jkl@johnluebs.com>
# Contributor: hdhoang <arch@hdhoang.space>

pkgname=nginx-mainline-mod-fancyindex
_modname="ngx-${pkgname#nginx-mainline-mod-}"
pkgver=0.5.1
pkgrel=12
pkgdesc="Fancy indexes module for the nginx web server"
arch=('x86_64')
url="https://github.com/aperezdc/ngx-fancyindex"
license=('custom')
depends=('nginx-mainline')
makedepends=('nginx-mainline-src')
source=("https://github.com/aperezdc/$_modname/archive/v$pkgver/$_modname-$pkgver.tar.gz")
sha256sums=('238bd5521d6c9b55780e6871339a7ea79508b9a6758ad2fa4451f2dfe26d94c9')

prepare() {
    cp -r /usr/src/nginx .
}

build() {
    cd nginx
    ./configure --with-compat --add-dynamic-module=../$_modname-$pkgver
    make modules
}

package() {
    install -Dm644 $_modname-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

    cd nginx/objs
    for mod in ngx_*.so; do
	install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
    done
}
