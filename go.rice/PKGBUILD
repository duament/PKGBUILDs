# Maintainer: Rvfg <i@rvf6.com>

pkgname=go.rice
pkgver=1.0.2
pkgrel=2
pkgdesc="Go package that makes working with resources such as html,js,css,images,templates, etc very easy."
arch=('x86_64')
url="https://github.com/GeertJohan/go.rice"
license=('BSD')
depends=('glibc')
makedepends=('go')
source=("https://github.com/GeertJohan/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('2eed960aa9531a87c82d2379677723bc0129bc9e387dc0dbb3bb70b78c928dd0')

build() {
    export CGO_CPPFLAGS="$CPPFLAGS"
    export CGO_CFLAGS="$CFLAGS"
    export CGO_CXXFLAGS="$CXXFLAGS"
    export CGO_LDFLAGS="$LDFLAGS"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

    cd $pkgname-$pkgver
    go build -ldflags="-linkmode=external -extldflags=$LDFLAGS" -o rice/rice ./rice
}

check() {
    cd $pkgname-$pkgver
    go test ./...
}

package() {
    cd $pkgname-$pkgver
    install -Dm755 rice/rice "$pkgdir"/usr/bin/rice
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
