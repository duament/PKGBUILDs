# Maintainer: Duama <admin@duama.top>
# Contributor: Stefan Majewsky <majewsky@gmx.net>

pkgname=mpv-mpris
pkgver=0.5
pkgrel=4
pkgdesc='MPRIS (Media Player Remote Interface Spec) plugin for mpv'
arch=('x86_64')
url='https://github.com/hoyon/mpv-mpris'
license=('MIT')
depends=('mpv' 'glib2' 'glibc' 'zlib' 'util-linux-libs' 'libffi' 'pcre')
source=("https://github.com/hoyon/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('673aff031e7cc741edea68d7b4b0103d7b031d4a55755abb9e1be5dd4ec4e969')

build() {
    cd $pkgname-$pkgver
    make
}

package() {
    install -Dm755 $pkgname-$pkgver/mpris.so $pkgdir/usr/lib/mpv/mpris.so

    install -dm755 $pkgdir/etc/mpv/scripts
    ln -s /usr/lib/mpv/mpris.so $pkgdir/etc/mpv/scripts/mpris.so
}

