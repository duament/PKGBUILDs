# Maintainer: Rvfg <i@rvf6.com>
# Contributor: Tom Hacohen <tom@stosb.com>
# Contributor: David Runge <dave@sleepmap.de>

pkgname=etesync-dav
pkgver=0.32.0
pkgrel=1
pkgdesc="A CalDAV and CardDAV adapter for EteSync"
arch=('any')
url="https://github.com/etesync/etesync-dav"
license=('GPL')
depends=('python-appdirs'
         'python-etesync'
         'python-etebase'
         'radicale>=3.0.0'
         'python-flask'
         'python-flask-wtf'
)
makedepends=('python-setuptools')
replaces=('python-radicale-storage-etesync')
source=("$pkgname-$pkgver.tar.gz::https://github.com/etesync/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('38dadb8543279c07ccfb9b4231cc1ca647f0c511015ca6ac15be81119528f7db')

build() {
  cd $pkgname-$pkgver

  python setup.py build
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root=$pkgdir --optimize=1 --skip-build
  install -Dm644 DESCRIPTION.rst $pkgdir/usr/share/doc/$pkgname/DESCRIPTION.rst
  install -Dm644 README.md $pkgdir/usr/share/doc/$pkgname/README.md
  install -Dm644 examples/systemd-user/etesync-dav.service $pkgdir/usr/lib/systemd/user/etesync-dav.service
}

