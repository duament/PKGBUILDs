# Maintainer: Duama <admin@duama.top>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ariel AxionL <i@axionl.me>
# Contributor: Luke Yue <lukedyue@gmail.com>

pkgname=clash
pkgver=1.4.2
pkgrel=1
pkgdesc="A rule based proxy in Go."
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/Dreamacro/clash"
license=('GPL3')
depends=('glibc')
makedepends=('go' 'git')
backup=("etc/${pkgname}.conf")
source=("clash.conf"
        "clash.service"
        "$pkgname-$pkgver.tar.gz::https://github.com/Dreamacro/clash/archive/v$pkgver.tar.gz")
sha256sums=('a1e0b93ea4f746c6d8c8d54e798397e2b6488bc1ab2c59cd064952e71344fa9b'
            '290bd393315805e327247db1706f346c1315e37dfcf90f8bc88687791aa98bc4'
            '7bd3708a071c4f78788dff96b9a4b187196976fe4079253f4af3d9db98145f1c')

build() {
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

    cd "$pkgname-$pkgver"
    go build -trimpath -ldflags "-X github.com/Dreamacro/clash/constant.Version=${pkgver}" -mod=readonly
}

check() {
    cd "${pkgname}-${pkgver}"
    go test github.com/Dreamacro/clash/...
}

package() {
    install -Dm755 $pkgname-$pkgver/$pkgname "$pkgdir/usr/bin/$pkgname"
    install -Dm644 $pkgname.conf "$pkgdir/etc/$pkgname.conf"
    install -Dm644 $pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
}

