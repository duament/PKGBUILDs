
pkgname=clash
pkgver=0.17.0
pkgrel=1
pkgdesc="A rule based proxy in Go."
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/Dreamacro/clash"
license=('GPL3')
depends=()
makedepends=('go' 'git')
backup=("etc/${pkgname}.conf")
install=${pkgname}.install
source=("clash.conf"
        "clash.service"
        "clash.install"
        "clash-sysusers.conf"
        "clash-tmpfiles.conf"
        "$pkgname-$pkgver.tar.gz::https://github.com/Dreamacro/clash/archive/v$pkgver.tar.gz")
sha256sums=('43bcfd9ebe94f8ea58254af74c5191ef39907b8a4ea1c335af8f9b3a4c3024a3'
            'b8c750016501db48c2caacfb17ab1b558294b8d76f51c46a9071dc6d646ba569'
            'c3407e0e01d39ca769b104a32645464c366da16d2d935f699ca621b6a55802cb'
            '2397c16eeb48a4992ee3a880369a8b53dfdb0c0d657438e4eff49a8e1efc52f3'
            '528d683f8c68c477cae6c0e135f7d208d36cbafd433fd5c805e225ec3480f275'
            'e51a45d37d21127de7547f5658bb78fc3ed737201d005ddbebfdce317b560f91')

build() {
    cd $pkgname-$pkgver
    go build .
}

package() {
    install -Dm755 $pkgname-$pkgver/$pkgname "$pkgdir/usr/bin/$pkgname"
    install -Dm644 $pkgname.conf "$pkgdir/etc/$pkgname.conf"
    install -Dm644 $pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
    install -Dm644 $pkgname-sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
    install -Dm644 $pkgname-tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}

