# Maintainer: Duama <admin@duama.top>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ariel AxionL <i@axionl.me>
# Contributor: Luke Yue <lukedyue@gmail.com>

pkgname=clash
pkgver=0.19.0
pkgrel=1
pkgdesc="A rule based proxy in Go."
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/Dreamacro/clash"
license=('GPL3')
depends=('glibc')
makedepends=('go-pie' 'git')
backup=("etc/${pkgname}.conf")
install=${pkgname}.install
source=("clash.conf"
        "clash.service"
        "clash.install"
        "clash-sysusers.conf"
        "clash-tmpfiles.conf"
        "$pkgname-$pkgver.tar.gz::https://github.com/Dreamacro/clash/archive/v$pkgver.tar.gz")
sha256sums=('43bcfd9ebe94f8ea58254af74c5191ef39907b8a4ea1c335af8f9b3a4c3024a3'
            'a611aa6afc28e2836f33deec3963cdba470ac65c57e680dfe22cf184589501b9'
            'e0b06dffcb14f0cb9db5269e6f5fea3d17c90a2464e56a8a6da98c9106a9005c'
            '2397c16eeb48a4992ee3a880369a8b53dfdb0c0d657438e4eff49a8e1efc52f3'
            '528d683f8c68c477cae6c0e135f7d208d36cbafd433fd5c805e225ec3480f275'
            'b479cc08ab17b43e005c1d70babf296f27572e92ae736aa416663be08b27f85a')

build() {
    cd "$pkgname-$pkgver"
    go build -trimpath -ldflags "-extldflags $LDFLAGS -X github.com/Dreamacro/clash/constant.Version=${pkgver} -w -s" -mod=readonly
}

check() {
    cd "${pkgname}-${pkgver}"
    go test github.com/Dreamacro/clash/...
}

package() {
    install -Dm755 $pkgname-$pkgver/$pkgname "$pkgdir/usr/bin/$pkgname"
    install -Dm644 $pkgname.conf "$pkgdir/etc/$pkgname.conf"
    install -Dm644 $pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
    install -Dm644 $pkgname-sysusers.conf "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
    install -Dm644 $pkgname-tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}

