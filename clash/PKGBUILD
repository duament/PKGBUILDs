# Maintainer: Duama <admin@duama.top>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ariel AxionL <i@axionl.me>
# Contributor: Luke Yue <lukedyue@gmail.com>

pkgname=clash
pkgver=1.6.5
pkgrel=1
pkgdesc="A rule based proxy in Go."
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/Dreamacro/clash"
license=('GPL3')
depends=('glibc')
makedepends=('go')
source=("$pkgname.service"
        "https://github.com/Dreamacro/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('290bd393315805e327247db1706f346c1315e37dfcf90f8bc88687791aa98bc4'
            '3b0af8e8c42f077f8cf4fe62c8f0da7b9170c85930680135834bac2f0e46cbce')

build() {
    export CGO_CPPFLAGS="$CPPFLAGS"
    export CGO_CFLAGS="$CFLAGS"
    export CGO_CXXFLAGS="$CXXFLAGS"
    export CGO_LDFLAGS="$LDFLAGS"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

    cd $pkgname-$pkgver
    go build -trimpath -ldflags "-linkmode=external -X github.com/Dreamacro/$pkgname/constant.Version=$pkgver" -mod=readonly
}

check() {
    cd $pkgname-$pkgver
    go test ./...
}

package() {
    install -Dm755 $pkgname-$pkgver/$pkgname "$pkgdir"/usr/bin/$pkgname
    install -Dm644 $pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
}
