
pkgname=python-setuptools-rust
_pkgname=setuptools-rust
pkgver=0.12.0
pkgrel=1
pkgdesc="Setuptools plugin for Rust support"
arch=('any')
url="https://github.com/PyO3/setuptools-rust"
license=('MIT')
depends=('python' 'python-setuptools' 'python-semantic-version' 'python-toml')
makedepends=('python-setuptools-scm' 'python-wheel')
source=("$_pkgname-$pkgver.tar.gz::https://github.com/PyO3/$_pkgname/archive/v$pkgver.tar.gz")
sha256sums=('84a8e7a56093a68a40fd37e9061c28696e59261107bf86790a01273a6af981a6')

build() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

  cd $_pkgname-$pkgver
  python setup.py build
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
