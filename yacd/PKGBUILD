# Maintainer: Rvfg <i@rvf6.com>

pkgname=yacd
pkgver=0.3.4
pkgrel=1
pkgdesc="Yet Another Clash Dashboard"
arch=('any')
url="https://github.com/haishanh/yacd"
license=('MIT')
depends=()
makedepends=('yarn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/haishanh/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('75e53e1261cfa3ae5d3abdaaa39254eb3a84a8e8240da70266b87ddae9fc4b15')

prepare() {
    cd $pkgname-$pkgver

    sed -i '/"@fontsource\//d' package.json
    sed -i "/import '@fontsource\//d" src/components/Root.tsx

    yarn install --frozen-lockfile
}

build() {
    cd $pkgname-$pkgver

    yarn run build
}

package() {
    cd $pkgname-$pkgver/public

    find . -type f -exec install -Dm644 {} "$pkgdir"/usr/share/webapps/$pkgname/{} \;
}
