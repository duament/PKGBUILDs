
pkgname=yacd
pkgver=0.1.3
pkgrel=1
pkgdesc="Yet Another Clash Dashboard"
arch=('any')
url="https://github.com/haishanh/yacd"
license=('MIT')
depends=()
makedepends=('yarn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/haishanh/yacd/archive/v$pkgver.tar.gz"
        "no_web_fonts.patch")
sha256sums=('5c0c5dcc612563536e31a90e4acb66a0f92f937e6bcb2231033235169470288f'
            '435ef3d821887ef2e8702c46f2dc0cdf50fb6d1be099caa77d91a7b6a8b2b1bd')

prepare() {
    cd $pkgname-$pkgver
    patch --forward --strip=1 --input="$srcdir/no_web_fonts.patch"
    yarn install
}

build() {
    cd $pkgname-$pkgver
    yarn run build
}

package() {
    install -dm755 $pkgdir/usr/share/webapps/$pkgname
    install -Dm644 $pkgname-$pkgver/public/* $pkgdir/usr/share/webapps/$pkgname/
}
