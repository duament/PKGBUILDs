# Maintainer: Duama <admin@duama.top>

pkgname=yacd
pkgver=0.3.2
pkgrel=1
pkgdesc="Yet Another Clash Dashboard"
arch=('any')
url="https://github.com/haishanh/yacd"
license=('MIT')
depends=()
makedepends=('yarn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/haishanh/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('0949e0bd1e421617f0c3e3ab31450898a4feef2392fc589e44d19ee24cc0eec1')

prepare() {
    cd $pkgname-$pkgver

    sed -i '/"fontsource-open-sans":/d' package.json
    sed -i '/"fontsource-roboto-mono":/d' package.json
    sed -i "/import '@fontsource\//d" src/components/Root.tsx

    yarn install --frozen-lockfile
}

build() {
    cd $pkgname-$pkgver

    yarn run build
}

package() {
    cd $pkgname-$pkgver/public

    find . -type f -exec install -Dm644 {} "$pkgdir"/usr/share/webapps/$pkgname/{} \;
}
