
pkgname=yacd
pkgver=0.1.15
pkgrel=1
pkgdesc="Yet Another Clash Dashboard"
arch=('any')
url="https://github.com/haishanh/yacd"
license=('MIT')
depends=()
makedepends=('yarn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/haishanh/yacd/archive/v$pkgver.tar.gz"
        "no_web_fonts.patch")
sha256sums=('276876aabbb06e0ec0c9b56b9bf2584f690298344cb943c7dd2c36d054a1d435'
            '78709f93808c8c0ae4666041c99a030c455c214aa178742c56a8924c7c438c85')

prepare() {
    cd $pkgname-$pkgver
    patch --forward --strip=1 --input="$srcdir/no_web_fonts.patch"
    yarn install
}

build() {
    cd $pkgname-$pkgver
    yarn run build
}

package() {
    install -dm755 $pkgdir/usr/share/webapps/$pkgname
    install -Dm644 $pkgname-$pkgver/public/* $pkgdir/usr/share/webapps/$pkgname/
}
