# Maintainer: Duama <admin@duama.top>

pkgname=yacd
pkgver=0.2.15
pkgrel=1
pkgdesc="Yet Another Clash Dashboard"
arch=('any')
url="https://github.com/haishanh/yacd"
license=('MIT')
depends=()
makedepends=('yarn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/haishanh/$pkgname/archive/v$pkgver.tar.gz"
        "no_web_fonts.patch")
sha256sums=('ba6aa13e55dbdd60223c5d76e480070bb9ba6e9cd06d295a3e3bc7dbfedf9c6b'
            'ae083966fbdf4ea2630e0b2c2a7119a1fafed0355a3109202a02d468b3d1c866')

prepare() {
    cd $pkgname-$pkgver

    sed -i '/"fontsource-open-sans":/d' package.json
    sed -i '/"fontsource-roboto-mono":/d' package.json

    patch --forward --strip=1 --input="$srcdir"/no_web_fonts.patch

    yarn install --frozen-lockfile
}

build() {
    cd $pkgname-$pkgver

    yarn run build
}

package() {
    cd $pkgname-$pkgver

    install -dm755 "$pkgdir"/usr/share/webapps/$pkgname
    install -Dm644 public/* "$pkgdir"/usr/share/webapps/$pkgname/
}
