
pkgname=yacd
pkgver=0.2.6
pkgrel=1
pkgdesc="Yet Another Clash Dashboard"
arch=('any')
url="https://github.com/haishanh/yacd"
license=('MIT')
depends=()
makedepends=('yarn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/haishanh/yacd/archive/v$pkgver.tar.gz"
        "no_web_fonts.patch")
sha256sums=('2ce19295d9accdde730a6cebfd3cd3d6667d3a01d3591a536749eebef4a4d49f'
            'aff765b34f9ab850c2f59f7def788024850e3457a9c6623a6190ea3f93229bd1')

prepare() {
    cd $pkgname-$pkgver
    patch --forward --strip=1 --input="$srcdir/no_web_fonts.patch"
    yarn install
}

build() {
    cd $pkgname-$pkgver
    yarn run build
}

package() {
    install -dm755 $pkgdir/usr/share/webapps/$pkgname
    install -Dm644 $pkgname-$pkgver/public/* $pkgdir/usr/share/webapps/$pkgname/
}
