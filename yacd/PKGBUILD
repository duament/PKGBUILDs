
pkgname=yacd
pkgver=0.2.14
pkgrel=1
pkgdesc="Yet Another Clash Dashboard"
arch=('any')
url="https://github.com/haishanh/yacd"
license=('MIT')
depends=()
makedepends=('yarn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/haishanh/yacd/archive/v$pkgver.tar.gz"
        "no_web_fonts.patch")
sha256sums=('113b11cec0f6596f9d943388b23137218c915cd9a450b51a193baf394ea58e65'
            'b1bb02490bb83a0dfe57a8124d05b6c74d4aa8430f5c8b1c4fc1b1cd886aee2b')

prepare() {
    cd $pkgname-$pkgver
    patch --forward --strip=1 --input="$srcdir/no_web_fonts.patch"
    yarn install
}

build() {
    cd $pkgname-$pkgver
    yarn run build
}

package() {
    install -dm755 $pkgdir/usr/share/webapps/$pkgname
    install -Dm644 $pkgname-$pkgver/public/* $pkgdir/usr/share/webapps/$pkgname/
}
