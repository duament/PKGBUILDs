# Maintainer: Duama <admin@duama.top>

pkgname=yacd
pkgver=0.3.3
pkgrel=1
pkgdesc="Yet Another Clash Dashboard"
arch=('any')
url="https://github.com/haishanh/yacd"
license=('MIT')
depends=()
makedepends=('yarn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/haishanh/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('c83bf53ca38a8ea941a634e1a1c65001b42a2e5d45b92bc6c76c717f4b394e13')

prepare() {
    cd $pkgname-$pkgver

    sed -i '/"@fontsource\//d' package.json
    sed -i "/import '@fontsource\//d" src/components/Root.tsx

    yarn install --frozen-lockfile
}

build() {
    cd $pkgname-$pkgver

    yarn run build
}

package() {
    cd $pkgname-$pkgver/public

    find . -type f -exec install -Dm644 {} "$pkgdir"/usr/share/webapps/$pkgname/{} \;
}
