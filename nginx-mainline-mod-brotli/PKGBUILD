# Maintainer: Duama <admin@duama.top>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mainline-mod-brotli
_modname="ngx_${pkgname#nginx-mainline-mod-}"
pkgver=1.0.0rc
pkgrel=9
pkgdesc="Brotli compression filter module for nginx-mainline"
arch=('x86_64')
depends=('nginx-mainline' 'brotli')
makedepends=('nginx-mainline-src')
url="https://github.com/google/ngx_brotli"
license=('custom')

source=("https://github.com/google/$_modname/archive/v$pkgver/$_modname-$pkgver.tar.gz")
sha256sums=('c85cdcfd76703c95aa4204ee4c2e619aa5b075cac18f428202f65552104add3b')

prepare() {
    cp -r /usr/src/nginx .

    cd $_modname-$pkgver
    sed 's@/usr/local@/usr@' -i config
}

build() {
    cd nginx
    ./configure --with-compat --add-dynamic-module=../$_modname-$pkgver
    make modules
}

package() {
    install -Dm644 $_modname-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

    cd nginx/objs
    for mod in ngx_*.so; do
        install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
    done
}
