# Maintainer: Duama <admin@duama.top>

pkgname=miniflux
_pkgname=v2
pkgver=2.0.32
pkgrel=1
pkgdesc="Minimalist and opinionated feed reader"
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/miniflux/v2"
license=('Apache')
depends=('glibc')
optdepends=('postgresql')
makedepends=('go')
backup=('etc/miniflux.conf')
source=("https://github.com/miniflux/$_pkgname/archive/$pkgver/$_pkgname-$pkgver.tar.gz"
        "miniflux.service")
sha256sums=('58a7d81c3a33804c73a90d41fa2e2fa7a7d7111ff931489346ee42bc17b7dbaf'
            '72ed11252df54cc281a21e3d6ce08fc2cf2e36b1db1540a050bad47f2c6c8c4c')

build() {
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

    cd $_pkgname-$pkgver
    go build -trimpath -ldflags "-linkmode=external -X miniflux.app/version.Version=$pkgver" -mod=readonly -o $pkgname
}

check() {
    cd $_pkgname-$pkgver
    go test ./...
}

package() {
    install -Dm755 $_pkgname-$pkgver/$pkgname "$pkgdir"/usr/bin/$pkgname
    install -Dm644 $_pkgname-$pkgver/packaging/$pkgname.conf "$pkgdir"/etc/$pkgname.conf
    install -Dm644 $pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
    install -Dm644 $_pkgname-$pkgver/miniflux.1 "$pkgdir"/usr/share/man/man1/miniflux.1
}
